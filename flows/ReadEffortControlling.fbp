# CouchDB database setup
'http://localhost:5984/default' -> URL Conn(WebService/CouchDB/OpenDatabase)
Conn() CONNECTION -> CONNECTION New(WebService/CouchDB/GetChanges)
Conn() CONNECTION -> CONNECTION ReadAtt(WebService/CouchDB/ReadDocumentAttachment)
Conn() CONNECTION -> CONNECTION ReadDoc(WebService/CouchDB/ReadDocument)

# Watch for changes
'filter=Proggis/byType' -> OPTION New()
'type=execution' -> OPTION New()
'state=data_received' -> OPTION New()
'attachment=effortfigures.xlsx' -> OPTION New()
'id' -> KEY GetId(GetObjectKey)

# Get the execution document when it appears
New() OUT -> IN GetId() OUT -> IN ReadDoc() OUT -> IN SplitDoc(Split)

# Read attachment from execution document and write to local file
'effortfigures.xlsx' -> ATTACHMENT ReadAtt()
'effortfigures.xlsx' -> FILENAME WriteFile(WriteFileRaw)
SplitDoc() OUT -> DOCUMENT ReadAtt() OUT -> IN WriteFile() FILENAME -> IN SplitFileName(Split)

# Load effort figures into database
'ReadEffortFigures.fbp' -> GRAPH EffortFigures(Graph)
Conn() CONNECTION -> CONNECTION.IN EffortFigures()
SplitDoc() OUT -> EXECDOC.IN EffortFigures()

SplitFileName() OUT -> IN Display(Output)
SplitFileName() OUT -> FILENAME.IN EffortFigures()
