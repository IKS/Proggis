'parsespreadsheet.fbp' -> GRAPH Reader(Graph)

'IKS_effortcontrolling.xlsx' -> READ.SOURCE Reader()

# Effort figures are contained in the second spreadsheet of the file
'1' -> GETSHEET.KEY Reader()

# We can skip some rows that contain unnecessary data
'2' -> SLICEROWS.BEGIN Reader()
'59' -> SLICEROWS.END Reader()

# The label of the row is contained in the third column
'2' -> GROUPBYROWLABEL.KEY Reader()

# After reading the label, remove three first columns, and everything after real data
'3' -> SLICECOLUMNS.BEGIN Reader()
'16' -> SLICECOLUMNS.END Reader()

# CouchDB ssettings
'http://localhost:5984/default' -> URL Connection(WebService/CouchDB/OpenDatabase) CONNECTION -> CONNECTION Save(WebService/CouchDB/SaveObject)

# Get entities and split them to individual packets
Reader() ENTITIZE.OUT -> IN SplitEntities(SplitArray)

# Extract participants from data
'Partner' -> ACCEPT FilterPartners(FilterProperty)
'Country' -> ACCEPT FilterPartners()
# ...and map them to JSON-LD
'Partner=_id' -> MAP MapPartners(MapProperty)
'Country=foaf:country' -> MAP MapPartners()
'@type=organization' -> PROPERTY SetPartnerProps(SetProperty)
'_id=foaf:name' -> PROPERTY CopyPartnerProps(DuplicateProperty)

# Save partners into database
SplitEntities() OUT -> IN FilterPartners() OUT -> IN MapPartners() OUT -> IN SetPartnerProps() OUT -> IN CopyPartnerProps() OUT -> IN Save()

# Extract tasks from data
'(\d+\.\d+)\sTask\:(.+)' -> REGEXP FilterTasks(FilterProperty)
'1=name' -> MAP FlattenTasks(FlattenObject)
'@type=task' -> PROPERTY SetTaskProps(SetProperty)
'name=_id' -> PROPERTY CopyTaskProps(DuplicateProperty)
'_id=(\d+\.\d+)\sTask\:(.+)=http://iks-project.eu/task/$1' -> REGEXP MapTaskUri(MapPropertyValue)
'value' -> PROPERTY RemoveTaskProps(RemoveProperty)
SplitEntities() OUT -> IN FilterTasks() OUT -> IN RemoveGroups(RemoveGroups) OUT -> IN SplitTasks(SplitArray) OUT -> IN CollectTasks(CollectGroups) OUT -> IN FlattenTasks() OUT -> IN CopyTaskProps() OUT -> IN MapTaskUri() OUT -> IN SetTaskProps() OUT -> IN RemoveTaskProps() OUT -> IN Save()

# Extract effort allocations from data
'Partner' -> KEY GroupByPartner(GroupByObjectKey)
'(\d+\.\d+)\sTask\:(.+)' -> REGEXP FilterAllocTasks(FilterProperty)
'0=task' -> MAP FlattenAllocTasks(FlattenObject)
'1=assignee' -> MAP FlattenAllocTasks()
'@type=effortallocation' -> PROPERTY SetAllocProps(SetProperty)
'(\d+\.\d+)\sTask\:(.+)=http://iks-project.eu/task/$1' -> REGEXP MapAllocTaskUri(MapPropertyValue)

# Save effort allocations into database
SplitEntities() OUT -> IN RemoveSheet(RemoveGroups) OUT -> IN GroupByPartner() OUT -> IN FilterAllocTasks() OUT -> IN CollectAllocTasks(CollectGroups) OUT -> IN FlattenAllocTasks() OUT -> IN SetAllocProps() OUT -> IN MapAllocTaskUri() OUT -> IN Save()
