# Give the location of Tika
'tika-app-0.9.jar' -> TIKA Read(ReadDocument)

# Give the location of the document
'IKS_effortcontrolling.xlsx' -> SOURCE Read()

# If reading fails, just display the error
Read() ERROR -> IN Display(Output)

# Parse the file to JSON, get add spreadsheets from it
Read() OUT -> IN Parse(ParseXml) 

# We're only interested in DIVs inside the BODY
'body' -> KEY GetBody(GetObjectKey)
'div' -> KEY GetDiv(GetObjectKey)
# Read DIVs and pass them forward
Parse() OUT -> IN GetBody() OUT -> IN GetDiv()

# The effort spreadsheet is the second one in the file
'1' -> KEY GetEffortTable(GetObjectKey)
# Spreadsheet title is in a H1
'h1' -> KEY GroupByTableId(GroupByObjectKey)
# Get only the effort spreadsheet, group data by it
GetDiv() OUT -> IN GetEffortTable() OUT -> IN GroupByTableId()

# Get spreadsheet table and the rows from it
'table' -> KEY GetTable(GetObjectKey)
'tbody' -> KEY GetTBody(GetObjectKey)
'tr' -> KEY GetTR(GetObjectKey)
GroupByTableId() OUT -> IN GetTable() OUT -> IN GetTBody() OUT -> IN GetTR() 

# We can skip some rows that contain unnecessary data
'2' -> BEGIN SliceRows(SliceArray)
'59' -> END SliceRows(SliceArray)
# Process each row individually and get the cells
'td' -> KEY GetTD(GetObjectKey)
GetTR() OUT -> IN SliceRows() OUT -> IN SplitRows(SplitArray) OUT -> IN GetTD()

# The label of the row is contained in the third column
'2' -> KEY GroupByRowLabel(GroupByObjectKey)
# After reading the label, remove three first columns, and everything after real data
'3' -> BEGIN SliceColumns(SliceArray)
'16' -> END SliceColumns()
# Group by the row label, and collect into objects
GetTD() OUT -> IN GroupByRowLabel() OUT -> IN SliceColumns() OUT -> IN Collect(CollectGroups)

# Turn the columns into objects
Collect() OUT -> IN Entitize(PropertiesToObjects)

# Mappings for some properties
'Partner=prj:assignee' -> MAP MapProps(MapProperty)
'Country=foaf:country' -> MAP MapProps()

# Display output
Entitize() OUT -> IN SplitEntities(SplitArray) OUT -> IN MapProps() OUT -> IN Display()
